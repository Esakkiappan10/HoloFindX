// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 24
        compileSdkVersion = 34
        targetSdkVersion = 34
        ndkVersion = "23.1.7779620"
        kotlinVersion = "1.9.22"
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.2.2")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.22")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects { subproject ->
    afterEvaluate {
        if (subproject.plugins.hasPlugin("com.android.library") ||
            subproject.plugins.hasPlugin("com.android.application")) {
            subproject.android.buildFeatures {
                buildConfig true
            }
        }

        if (subproject.plugins.hasPlugin("com.android.library")) {
            def androidExt = subproject.extensions.findByName("android")
            if (androidExt != null && androidExt.namespace == null) {
                def manifestFile = file("${subproject.projectDir}/src/main/AndroidManifest.xml")
                if (manifestFile.exists()) {
                    def text = manifestFile.getText("UTF-8")
                    def matcher = text =~ /package=\"([^\"]+)\"/
                    if (matcher.find()) {
                        androidExt.namespace = matcher.group(1)
                    }
                }
            }
        }

        subproject.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = "11"
            }
        }

        subproject.plugins.withId("java") {
            subproject.extensions.findByType(
                org.gradle.api.plugins.JavaPluginExtension
            )?.toolchain {
                languageVersion = JavaLanguageVersion.of(11)
            }
        }
    }
}

apply plugin: "com.facebook.react.rootproject"
